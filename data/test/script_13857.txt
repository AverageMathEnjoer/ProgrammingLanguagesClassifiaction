b"{-# LANGUAGE InstanceSigs #-}\n{-# OPTIONS_GHC -fno-warn-orphans #-}\n-- |\n-- Copyright  : (c) Ivan Perez and Manuel Baerenz, 2016\n-- License    : BSD3\n-- Maintainer : ivan.perez@keera.co.uk\n--\n-- Instance of 'ArrowChoice' for Monadic Stream Functions ('MSF').\n--\n-- Import this module to include that (orphan) instance.\nmodule Data.MonadicStreamFunction.Instances.ArrowChoice where\n\n-- External imports\nimport Control.Arrow (ArrowChoice (..))\n\n-- Internal imports\nimport Data.MonadicStreamFunction.Core         ()\nimport Data.MonadicStreamFunction.InternalCore (MSF (MSF, unMSF))\n\n-- | 'ArrowChoice' instance for MSFs.\ninstance Monad m => ArrowChoice (MSF m) where\n  left :: MSF m a b -> MSF m (Either a c) (Either b c)\n  left sf = MSF f\n    where\n      f (Left a) = do (b, sf') <- unMSF sf a\n                      return (Left b, left sf')\n      f (Right c) = return (Right c, left sf)\n"